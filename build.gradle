import java.time.Year

plugins {
    id 'java-library'
    id 'com.diffplug.spotless' version "7.0.2"
}

def loadEnv() {
    def envFile = file(".env")
    if (envFile.exists()) {
        envFile.eachLine { line ->
            if (line && !line.startsWith("#") && line.contains("=")) {
                def parts = line.split("=", 2)
                if (parts.length == 2) {
                    def key = parts[0].trim()
                    def value = parts[1].trim()
                    ext.set(key, value)
                }
            }
        }
    }
}

loadEnv()

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.diffplug.spotless'

    java {
        sourceCompatibility = JavaVersion.VERSION_21

    }

    spotless {
        java {
            googleJavaFormat('1.17.0')
            licenseHeader rootProject.file('licenseHeader.txt').text.replace('${YEAR}', Year.now().value.toString())
            target '*.java'
            importOrder()
            removeUnusedImports()
            leadingTabsToSpaces(4)
            trimTrailingWhitespace()
            palantirJavaFormat()
        }
    }

    test {
        useJUnitPlatform()
    }
}



tasks.jar {
    exclude 'org/smc4j/example/**'
}
